# Switch #

>Основной метод ay_sw имеет класс аномалии **"Фатальный"** - [Метки в сокращениях](../../../documents/CCA-5-1.md#Метки%20в%20сокращениях)
>
>Данный модуль не рекомендуется к использованию

Версия: **0.1**

Название книги: **Anatoliy57_switch**

Соблюденные соглашения:

* [CCA-1](../../../documents/CCA-1.md)
* [CCA-2](../../../documents/CCA-2.md)
* [CCA-3](../../../documents/CCA-3.md)
* [CCA-4](../../../documents/CCA-4.md)
* [CCA-5](../../../documents/CCA-5.md)

Рабочий код можно посмотреть [здесь](additional/switchUI-code.md)

Заклинание актуально на момент ***06.09.19***

## Глобальные переменные ##

* !x31 (далее id) - используется для подсчета кол-ва использования метода SW_ID для создания уникальных ссылок.

***

## Использование ##

Данный модуль позволяет динамически создавать ссылки на метки (\>метка), что позволяет организовать такую структуру как [switch](https://ru.wikipedia.org/wiki/Ветвление_(программирование)#Переключатель), альтернатива if ... else if ... else if ... Для этой цели используется основной метод **SW**, **SW_P**:

```cadabra
am ****1 >ay_sw *** textos игнорируется x

1: textos 1 x

2: textos 2 x

3: textos 3 x

4: textos 4 x
```

>Вывод будет: 1.

Можно зайти дальше:

```cadabra
am !tri cyklix >ay_sw_p *** !!! plux du reprizix x

unu: textos 1 <

du: textos 2 <

tri: textos 3 <

kvar: textos 4 <

kvin: textos 5 <

ses: textos 6 <

sep: textos 7 <
```

>Вывод будет: 1, 3, 5.

С помощью такого подхода можно достичь интересных результатов.

>**Примечание:** подобную генерацию можно сделать лишь с переходами. Генерацию же меток невозможно, т. к. они регистрируются на момент начало заклинания.

***

Данный модуль предоставляет не только возможности использования конструкции switch но и возможность многократного использования некоторых модулей.

К примеру у вас на руках модуль, который используется внутри себя метки и из-за этого использует строго определенную метку, что не позволяет использовать его дважды. Тут на помощь поможет нам метод **SW_ID**.

Лучше всего понимается на примерах:

```cadabra
am ****test >test test_id_unu: textos один >test test_id_du: textos два x

### Модуль ###

am test:
... >subtest ...
****test >ay_sw_id ***

subtest: ... <
```

>Вывод: один, два.

Как было сказано выше в примечании, автогенерация меток невозможна, поэтому подобного фокуса сделать в циклах и тому подобное невозможно. Такой способ стоит использовать, когда вам точно известно сколько будете использовать подобный модуль.

>Большинство (все) такой способ не применяют, вам придется скорее всего исправлять исходник модуля для поддержки такой функции.

Есть еще несколько методов как **SW_GET_ID, SW_SET_ID и SW_SH_ID**. Они носят чистый информативный, конфигурационный характер, их описание можете прочитать ниже под заголовком [Описание методов](#Описание-методов).

***

## Приложения ##

[**Код switchUI**](additional/switchUI-code.md)

Существуют модули, отлично проявляющие свои возможности в связке с **switchUI**.
Вот весь их список (автор **Anatoliy57**):

* [ShortSwP](additional/shortSwP-code.md) позволяет использовать мощности порядка от 20 до 89

  Пример:

  ```cadabra
  >am !!!!kvin >p_s_50 !!!!unu >p_g_50 textos !!!!
  ```

  >Вывод: 5.

* [ShortSwS](additional/shortSwS-code.md) позволяет использовать слова порядка от 20 до 89

  Пример:

  ```cadabra
  >am ****hello >p_s_50 ****bb >p_g_50 textos ****
  ```

  >Вывод: hello.

* [swP](additional/swP-code.md) позволяет использовать мощности порядка от 20 до 89

  Пример:

  ```cadabra
  >am !!!!kvin >p_s_kvindek !!!!unu >p_g_kvindek textos !!!!
  ```

  >Вывод: 5.

* [SwS](additional/swS-code.md) позволяет использовать слова порядка от 20 до 89

  Пример:

  ```cadabra
  >am ****hello >p_s_kvindek ****bb >p_g_kvindek textos ****
  ```

  >Вывод: hello.

***

## Описание методов ##

* [SW](#SW)

  Аномалия класса **"Фатальный"** - [Метки в сокращениях](../../../documents/CCA-5-1.md#Метки%20в%20сокращениях)
  
  Класс: **safe - easy | easy**

  Параметры:

  * **\*х4** - *название метки*

  Выход:

  * **\*х3** - *ссылка на метку*

  Преобразует название метки в рабочую ссылку.

* [SW_P](#SW_P)

  Аномалия класса **"Фатальный"** - [Метки в сокращениях](../../../documents/CCA-5-1.md#Метки%20в%20сокращениях)
  
  Класс: **safe - easy | easy**

  Параметры:

  * **!х3** - *название метки в виде числа*

  Выход:

  * **\*х3** - *ссылка на метку*

  То же самое что и *SW*.

* [SW_ID](#SW_ID)

  Аномалия класса **"Фатальный"** - [Метки в сокращениях](../../../documents/CCA-5-1.md#Метки%20в%20сокращениях)

  Класс: **safe - easy | easy**

  Параметры:

  * **\*х4** - *название метки*

  Выход:

  * **\*х3** - *ссылка на метку*

  Работает схожим образом как *SW*, за тем исключением, что к названию ссылки прибавляется суффикс **\_id\_"номер"**. Номер с каждым использованием SW\_ID увеличивается на один.

* [SW_GET_ID](#SW_ID_GET)

  Класс: **easy**

  Выход:

  * **!** - *id*

  Возвращает текущее значение id.

* [SW_SET_ID](#swsetid)

  Класс: **safe - easy**

  Параметры:

  * **!** - *новый id*

  Устанавливает новый id.

* [SW_SH_ID](#swshid)

  Выводит значение id.

***

## Методы ##

### SW ###

|Параметры|        | \| | Выход   |        |
|---------|--------|:--:|---------|--------|
| *x4     | mark   | \| | *x3     | goto   |

```cadabra
ay_sw:

  ***#> plux ****
  *** minux #

<
```

### SW_P ###

|Параметры|        | \| | Выход   |        |
|---------|--------|:--:|---------|--------|
| !x3     | mark   | \| | *x3     | goto   |

```cadabra
ay_sw_p:

  ***#> plux !!!
  *** minux #

<
```

## SW_ID ##

|Параметры|        | \| | Выход   |        |
|---------|--------|:--:|---------|--------|
| *x4     | mark   | \| | *x3     | goto   |

```cadabra
ay_sw_id:

  ***#> plux ****
  *** plux _id_
  *** plux !x31
  *** minux #
  !x31 plux unu

<
```

## SW_GET_ID ##

| Выход |      |
|-------|------|
| !     | id   |

```cadabra
ay_sw_get_id:

  ! ekvivax !x31

<
```

## SW_SET_ID ##

|Параметры|        |
|---------|--------|
| !       | new_id |

```cadabra
ay_sw_set_id:

  !x31 ekvivax !

<
```

## SW_SH_ID ##

```cadabra
ay_sw_sh_id:

  textos !x31

<
```

***

>[Каталог](../../../Список%20заклинаний.md)
